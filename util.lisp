(cl:in-package :fmt.internal)

(eval-when (:compile-toplevel :load-toplevel :execute)
  (setf (fdefinition 'eq?) #'eq)
  (setf (fdefinition 'odd?) #'oddp)
  (setf (fdefinition 'integer?) #'integerp)
  (setf (fdefinition 'list?) #'listp)
  (setf (fdefinition 'negative?) #'minusp)
  (setf (fdefinition 'null?) #'null)
  (setf (fdefinition 'pair?) #'consp)
  (setf (fdefinition 'positive?) #'plusp)
  (setf (fdefinition 'zero?) #'zerop)
  (setf (fdefinition 'vector-length) #'length)
  (setf (fdefinition 'vector?) #'vectorp)
  (setf (fdefinition 'procedure?) #'functionp)
  (setf (fdefinition 'exact?) #'rationalp)
  (setf (fdefinition 'inexact->exact) #'rationalize)
  (setf (fdefinition 'even?) #'evenp)
  (setf (fdefinition 'real?) #'realp)
  (setf (fdefinition 'newline) #'terpri)
  (setf (fdefinition 'display) #'princ)
  (setf (fdefinition 'remainder)  #'rem)
  (setf (fdefinition 'string-length)  #'length)
  (setf (fdefinition 'char->integer)  #'char-code)
  (setf (fdefinition 'string-ref) #'char)
  (setf (fdefinition 'symbol->string) #'string)
  (setf (fdefinition 'string?) #'stringp)
  (setf (fdefinition 'symbol?) #'stringp)
  (setf (fdefinition 'number?) #'numberp)
  (setf (fdefinition 'char?) #'characterp)
  (setf (fdefinition 'real-part) #'realpart)
  (setf (fdefinition 'imag-part) #'imagpart)
  (setf (fdefinition 'string=?) #'string=)
  (setf (fdefinition 'string-ci=?) #'string-equal)
  )

(defmacro set! (var val)
  `(setq ,var ,val))

(declaim (inline list-tail vector-set! list-ref vector->list list->vector
                 quotient set-car! set-cdr! eqv? equal?
                 assq assv assoc for-each))

(defun exact->inexact (exact)
  (float exact 0d0))

(defun boolean? (obj)
  (typep obj 'boolean))

(defun for-each (fn &rest lists)
  (apply #'mapc fn lists)
  nil)

(defun assq (item alist)
  (cl:assoc item alist :test #'eq?))

(defun assv (item alist)
  (cl:assoc item alist :test #'eqv?))

(defun assoc (item alist)
  (cl:assoc item alist :test #'equal?))

(defun eqv? (x y)
  (eql x y))

(defun equal? (x y)
  (equal x y))

(defun set-car! (list obj)
  (rplaca list obj))

(defun set-cdr! (cons x)
  (rplacd cons x))

(defun quotient (x y)
  (values (truncate x y)))

(defun list-tail (list k)
  (nthcdr k list))

(defun list-ref (list k)
  (nth k list))

(defun vector-set! (vec index val)
  (setf (aref vec index) val))

(defun vector->list (vec)
  (coerce vec 'list))

(defun list->vector (list)
  (coerce list 'vector))

(eval-when (:compile-toplevel :load-toplevel :execute)
  (defun to-proper-lambda-list (list)
    (typecase list
      (list (if (tailp () list)
                list
              (cl:let ((last (last list)))
                `(,@(butlast list)
                  ,(car last)
                  cl:&rest
                  ,(cdr last)))))
      (symbol `(cl:&rest ,list)))))

(defmacro lambda (args &rest body)
  `(cl:lambda ,(to-proper-lambda-list args)
     ,@body))

(defmacro letrec ((&rest binds) &body body)
  `(let (,@(mapcar (cl:lambda (x)
                     `(,(car x) #'values))
             binds))
     (declare (optimize (space 3)))
     (labels (,@(remove nil
                  (mapcar (cl:lambda (x &aux (name (car x)))
                            `(,name
                               (&rest args)
                               (apply ,name args)))
                          binds)))
       (declare (optimize (debug 0) (space 3)))
       (psetq ,@(apply #'append binds))
       ,@body)))


(defmacro define-function (name-args &body body)
  (if (consp name-args)
      (destructuring-bind (name . args)
                          name-args
        `(defun ,name ,(to-proper-lambda-list args)
           ,@body))
      `(progn
         (setf (fdefinition ',name-args)
               ,(car body)))))

(declaim (inline vector-ref))
(defun vector-ref (vec k)
  (svref vec k))

(declaim (inline modulo))
(defun modulo (x y)
  (mod x y))

(defmacro begin (&body body)
  `(progn ,@body))

(declaim (inline make-vector))
(defun make-vector (size &optional (init 0))
  (cl:make-array size                   ;***
                 :initial-element init
                 :adjustable nil
                 :fill-pointer nil))

(declaim (inline string-append))
(defun string-append (&rest strings)
  (format nil "窿篝蜷铉螬ㄤ邈灬轫ㄩ铎轭铛礅弪倔趄轭绌ㄤ彐躅铛礅弪倔趄轭铛愆黩轸瀛麸篝蜷铉铛愆ㄤ彐磲泸滹戾è蝈篝鲠蜢轶舂孱潇轶怙澌怙澌戾舄è鲠蝮磲疸狎灬礅溽雯ㄩㄣ镱箴雯ㄣ狎雯雯鲠蜢轶┅ㄢ轭潴磲疸狎灬礅溽ㄢㄩㄣ镱箴猢ㄤ弩趄蹉趱蜷铉忾钿鲠镳糸镱犰轭轸铄舂ㄩ铄啜鲠轭轸戾ì括磲疸狎灬礅溽扉篝鲠蝮┅ㄤ邈灬蝈ㄩ珙矧徕戾丽狎螬铄┅啜鲠轭轸┅扉篝铋飑┅鲠蜢轶┅啜沆轰忾钿孱潇轶棱镤┅ㄤ彐躅怩綮狍翎麸扉篝啜括怩綮狍扉篝ㄣ狎灬篝扉篝┅┅ㄤ彐磲泸戾舡镳糸镱犰螵ㄡ蜱é蝈篝忾钿螬怙澌怙澌戾è蝈篝ㄩ翎殪Ж忾钿螬Ж啜蝈篝ㄣ潋灬篝忾钿螬┅┅ㄣ旌祜镳烘矧鲠鲠飓痱邃洪磲疸狎灬礅溽ㄩㄣ镱箴扉篝┅ㄩ蝈篝ㄢ豸灬篝狒镯忾钿螬忾钿螬烘矧烘蝻恒镬戾泗ㄩㄣ潋鲠飓痱邃啜鲠矧ㄡ钿铘鲠飓痱邃鲠颟铘鲠飓痱邃┅啜鲠矧铘狎珞铘鲠飓痱邃┅洪铘鲠颢溴驷蹯恒镬戾泗鲠洪铘铄鳝忾钿烘轭犰禊蝈趱蝾啜溴篝蝓泗躜轭绛忾钿é镳糸镱犰李鬻忾钿莉弩舂狎珞ㄤ邈灬蝈ㄩ珙矧徕戾李鬻忾钿螬戾舄ì丽狎溴驷蹯舂棱镤┅┅┅ㄤ彐躅磲脲羼凯翎忪ī磲脲栳箬翎忪＇羼咯ㄤ彐躅轭屮徙艨瞟铒蜥糸镱犰瞟┅换屣